/* rexx */
/* trace i */
/*********************************************************************/
/* This exec invokes IBM's QMF dialog for QMF Version 6 and higher.  */
/* The DSQLLIB DD will be used to concatenate all necessary load     */
/* libraries, so you should no longer need Dynamic STEPLIB to enable */
/* multiple QMF subsystems.  The ztrail variable is used to prime    */
/* the DSQSSUBS value on the ISPEXEC SELECT statement.               */
/*********************************************************************/
parse arg ztrail
msgsave = msg()
x = msg('OFF')
address tso "ALLOC FI(ADMCFORM) DA('qmfpref.DSQCHART') SHR REUSE"
address tso "ALLOC FI(ADMCDATA) DA('GDDM.SADMCDA') SHR REUSE"
address tso "ALLOC FI(ADMGDF)   DA('GDDM.SADMGDF') SHR REUSE"
address tso "ALLOC FI(ADMGGMAP) DA('qmfpref.DSQMAPE') SHR REUSE"
address tso "ALLOC FI(ADMSYMBL) DA('GDDM.SADMSYM') SHR REUSE"
address tso "ALLOC FI(DSQDEBUG) DA(*) SHR REUSE"
address tso "ALLOC FI(DSQEDIT)  NEW UNIT(SYSALLDA) CYL SPACE(1 1)",
            "DSORG(PS) RECFM(F B A) LRECL(79) BLKSIZE(4029)"
address tso "ALLOC FI(DSQLLIB) DA('qmfpref.SDSQLOAD',",
                                 "'db2pref.SDSNEXIT',",
                                 "'db2pref.SDSNLOAD') SHR REUSE"
address tso "ALLOC FI(DSQPNLE)  DA('qmfpref.DSQPNLE') SHR REUSE"
address tso "ALLOC FI(DSQPRINT) DA(*) SHR REUSE"
address tso "ALLOC FI(DSQSPILL) NEW UNIT(SYSALLDA) CYL SPACE(1 1)",
            "DSORG(PS) RECFM(F) LRECL(4096) BLKSIZE(4096)"
address tso "ALLOC FI(DSQUCFRM) DA('"userid()".QMF.DSQUCFRM') SHR",
            "REUSE"
if rc <> 0 then
   address tso "ALLOC FI(DSQUCFRM) DA('"userid()".QMF.DSQUCFRM') NEW",
               "CATALOG UNIT(SYSALLDA) CYL SPACE(1 1) DIR(45)",
               "DSORG(PO) RECFM(F) LRECL(400) BLKSIZE(400)"
address tso "ALLOC FI(DSQUDUMP) SYSOUT(A) RECFM(V B A) LRECL(125)",
            "BLKSIZE(1632)"
address tso "ALLOC FI(SYSUDUMP) SYSOUT(A)"
address tso "ALTLIB ACT APPL(CLIST) DA('qmfpref.SDSQCLTE')"
address tso "ALTLIB ACT APPL(EXEC) DA('qmfpref.SDSQEXCE')"
address ispexec "LIBDEF ISPLLIB LIBRARY ID(DSQLLIB) STACK"
address ispexec "LIBDEF ISPMLIB DATASET ID('qmfpref.SDSQMLBE') STACK"
address ispexec "LIBDEF ISPPLIB DATASET ID('qmfpref.SDSQPLBE') STACK"
address ispexec "LIBDEF ISPSLIB DATASET ID('qmfpref.SDSQSLBE') STACK"
address ispexec "SELECT PGM(DSQQMFE) NEWAPPL(DSQE) PASSLIB NOCHECK",
                "SCRNAME(QMF) PARM(S="ztrail")"
address ispexec "LIBDEF ISPLLIB"
address ispexec "LIBDEF ISPMLIB"
address ispexec "LIBDEF ISPPLIB"
address ispexec "LIBDEF ISPSLIB"
address tso "ALTLIB DEACT APPL(EXEC)"
address tso "ALTLIB DEACT APPL(CLIST)"
address tso "FREE FI(ADMCDATA ADMCFORM ADMGDF   ADMSYMBL ADMGGMAP)"
address tso "FREE FI(DSQDEBUG DSQEDIT  DSQLLIB  DSQPNLE  DSQPRINT)"
address tso "FREE FI(DSQSPILL DSQUCFRM DSQUDUMP SYSUDUMP)"
x = msg(msgsave)
