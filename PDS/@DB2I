/* rexx */
/* trace i */
/*********************************************************************/
/* This exec invokes IBM's DB2I dialog.  This exec uses both ssid    */
/* and ztrail to hold the DB2 subsystem id and the nested menu       */
/* option, respectively (the nested menu option requires the         */
/* modified copy of DSNECPRI to work).  This exec shows a sample of  */
/* how to use ssid in the dataset names to allocate the correct      */
/* system-specific DB2 datasets (ssid must be 4 characters or less). */
/*********************************************************************/
parse arg ssid ztrail
address tso "ALTLIB ACT APPL(CLIST) DA('pref.DYNISPF.EXEC',",
                                      "'DB2."ssid".SDSNCLST')"
address ispexec "LIBDEF ISPLLIB DATASET ID ('DB2."ssid".SDSNEXIT',",
                                           "'DB2."ssid".SDSNLOAD')",
                                       "STACK"
address ispexec "LIBDEF ISPMLIB DATASET ID ('DB2."ssid".SDSNSPFM')",
                                       "STACK"
address ispexec "LIBDEF ISPPLIB DATASET ID ('DB2."ssid".SDSNSPFP',",
                                           "'DB2."ssid".SDSNPFPE')",
                                       "STACK"
address ispexec "LIBDEF ISPSLIB DATASET ID ('DB2."ssid".SDSNSPFS')",
                                       "STACK"
address ispexec "LIBDEF ISPTLIB DATASET ID ('DB2."ssid".SDSNSPFT')",
                                       "STACK"
ztrails = ''
if ztrail <> '' then
   ztrails = 'ZTRAIL('ztrail')'
address ispexec "SELECT CMD(%DSNECPRI SSID("ssid")" ztrails ")",
                "NEWAPPL(DSNE) PASSLIB SCRNAME(DB2I"ssid")"
address ispexec "LIBDEF ISPLLIB"
address ispexec "LIBDEF ISPMLIB"
address ispexec "LIBDEF ISPPLIB"
address ispexec "LIBDEF ISPSLIB"
address ispexec "LIBDEF ISPTLIB"
address tso "ALTLIB DEACT APPL(CLIST)"
