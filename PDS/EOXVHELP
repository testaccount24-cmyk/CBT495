/*REXX*/
/*-------------------------------------------------------------------*/
/* This exec has been modified for Dynamic ISPF to add the PASSLIB   */
/* parm to the SELECT NEWAPPL statement.                             */
/*-------------------------------------------------------------------*/
/* APAR PN20713 states IBM's official position that BookManager READ */
/* does not support LIBDEF's.  PN20713 goes on to state that IBM     */
/* will not support anyone wishing to use LIBDEF when invoking       */
/* Book Manager READ.  IBM cites four reasons as the basis for       */
/* denying LIBDEF support in BookManager READ.  In a Dynamic ISPF    */
/* environment, only one of the four reasons is valid.  The fourth   */
/* reason listed in PN20713 describes the BookManager "Edit source"  */
/* function.  This function performs a split screen and brings up an */
/* edit session so the user can directly edit the manual they're     */
/* working with.  This function will not work because the LIBDEF's   */
/* are not carried across the split screen.  If you require the      */
/* "Edit source" function in BookManager, you will have to create a  */
/* LOGON proc and hard allocate the BookManager datasets.  If you do */
/* not require the "Edit source" function, this exec will correctly  */
/* invoke BookManager READ, but you do so at your own risk.  IBM     */
/* will not support any dialog errors you encounter unless you have  */
/* hard allocated datasets in a LOGON PROC.                          */
/*-------------------------------------------------------------------*/
/*  06/04/92  APAR PN22864 - Make Numeric Digits for address dynamic */
/*  04/08/92  APAR PN18465 - Incorrect call to routine d2x           */
/*-------------------------------------------------------------------*/
/*  BookManager Read/MVS * 5695-046 *                                */
/*  (C) COPYRIGHT IBM CORPORATION 1990, 1994                         */
/*  LICENSED MATERIAL - PROGRAM PROPERTY OF IBM                      */
/*  REFER TO COPYRIGHT INSTRUCTIONS: FORM G120-2083                  */
/*-------------------------------------------------------------------*/
/*                                                                   */
/* Exec Name:    EOXVHELP                                            */
/*                                                                   */
/* Date Written: 01/12/91                                            */
/*                                                                   */
/* Description:  Called to open the BookManager User's Guide to      */
/*               the location associated with the current panel.     */
/*               If there is no information about this panel, just   */
/*               open to the TOC.                                    */
/*                                                                   */
/* Invocation:   EOXVHELP                                            */
/*                                                                   */
/* Parameters:   ISPF variable EOXVHELP in the shared pool has the   */
/*               address of the last ISPF DISPLAY PANEL(...)         */
/*               command issued.                                     */
/*                                                                   */
/*-------------------------------------------------------------------*/
trace off
/* NUMERIC DIGITS 10;                 APAR PN18465 Fix: 32-bit addr. */
ISPEXEC "VGET (EOXVHELP) SHARED"   /* Get the addr of the last       */
if (eoxvhelp = 0) then             /* DISPLAY PANEL command. If 0,   */
  do                               /* it means the User Guide is not */
    ISPEXEC "SETMSG MSG(EOXA500)"  /* available.                     */
    return
  end
"%EOXVOPTS"                        /* Ensure options set             */
ISPEXEC "VGET (QHELPDSN)"          /* Get HELP data set name         */
qhelpdsn = strip(qhelpdsn)
if (qhelpdsn = "") then
  do
    ISPEXEC "SETMSG MSG(EOXA500)"
    return
  end
qhelpdsn = strip(qhelpdsn,,"'")
NUMERIC DIGITS max(length(eoxvhelp),9)       /* APAR PN22864 Fix */
cmd = storage(d2x(eoxvhelp),255) /* Get last DISPLAY panel       */
i = pos("PANEL",cmd)
if (i = 0) then
  cmd = ""
else
  do
    cmd = substr(cmd,i+5) || "?"
    do while(left(cmd,1) = " ")
      cmd = substr(cmd,2)
    end
    if (left(cmd,1) ¬= "(") then
      cmd = ""
    else
      do
        i = pos(")",cmd)
        if (i ¬= 0) then
         cmd = substr(cmd,2,i-2)
        else
         cmd = substr(cmd,2)
        cmd = strip(cmd)
      end
  end
if (cmd ¬= "") then                /* If found name, do link to it.  */
  cmd = "CMD(EOXCMDN6 SPOT" cmd || ")"  /* Otherwise TOC.            */
cmd = "BOOK('"||qhelpdsn||"')" cmd /* Add DSN for user guide         */
/* Invoke BookManager again. Open the User Guide.                    */
ISPEXEC "CONTROL NONDISPL ENTER"   /* Prevent the logo from showing  */
ISPEXEC "SELECT CMD(%EOXVSTRT" cmd ") MODE(FSCR) SUSPEND NEWAPPL(EOXR)",
        "PASSLIB"
return                             /* Return to caller               */
