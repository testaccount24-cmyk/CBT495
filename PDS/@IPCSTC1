/* rexx */
/* trace i */
/*********************************************************************/
/* This exec invokes IBM's IPCS dialog with TCP/IP support.  It is   */
/* provided courtesy of Phil Knight at AIG.  Code is added to        */
/* determine the z/OS release, since TCP/IP has repackaged at both   */
/* z/OS V1R2 and z/OS V1R4.                                          */
/*********************************************************************/
parse arg ztrail
/*********************************************************************/
/* At z/OS V1R4 and higher, the CLIST library is SYS1.SBLSCLI0.      */
/*********************************************************************/
if mvsvar('sysmvs') >= 'SP7.0.4' then
   address tso "ALTLIB ACT APPL(CLIST) DA('SYS1.SBLSCLI0')"
else
   address tso "ALTLIB ACT APPL(CLIST) DA('TCPIP.SEZAINST',",
                                         "'SYS1.SBLSCLI0')"
address tso "%BLSCDDIR"
/*********************************************************************/
/* At z/OS V1R2 and higher, the load library is SYS1.MIGLIB.         */
/*********************************************************************/
if mvsvar('sysmvs') >= 'SP7.0.2' then
   address ispexec "LIBDEF ISPLLIB DATASET ID('SYS1.MIGLIB') STACK"
else
   address ispexec "LIBDEF ISPLLIB DATASET ID('TCPIP.SEZAMIG') STACK"
/*********************************************************************/
/* At z/OS V1R4 and higher, the ISPF elements are all in SYS1.SBLS*. */
/*********************************************************************/
if mvsvar('sysmvs') >= 'SP7.0.4' then
   do
      address ispexec "LIBDEF ISPMLIB DATASET ID('SYS1.SBLSMSG0') STACK"
      address ispexec "LIBDEF ISPPLIB DATASET ID('SYS1.SBLSPNL0') STACK"
      address ispexec "LIBDEF ISPTLIB DATASET ID('pref.DYNISPF.EXEC',",
                                                "'SYS1.SBLSTBL0') STACK"
   end
else
   do
      address ispexec "LIBDEF ISPMLIB DATASET ID('SYS1.SBLSMSG0',",
                                                "'TCPIP.SEZAMENU')",
                                                "STACK"
      address ispexec "LIBDEF ISPPLIB DATASET ID('SYS1.SBLSPNL0',",
                                                "'TCPIP.SEZAPENU')",
                                                "STACK"
      address ispexec "LIBDEF ISPTLIB DATASET ID('pref.DYNISPF.EXEC',",
                                                "'SYS1.SBLSPNL0',",
                                                "'SYS1.SBLSTBL0') STACK"
   end
address ispexec "LIBDEF ISPSLIB DATASET ID('SYS1.SBLSKEL0') STACK"
address ispexec "SELECT PGM(BLSG) NEWAPPL(EZBD) SCRNAME(IPCSTCP)",
                "PARM(PGM(BLSGSCMD)",
                "PARM(EXEC 'pref.DYNISPF.EXEC(@IPCSMNU)')) PASSLIB"
address ispexec "LIBDEF ISPLLIB"
address ispexec "LIBDEF ISPMLIB"
address ispexec "LIBDEF ISPPLIB"
address ispexec "LIBDEF ISPSLIB"
address ispexec "LIBDEF ISPTLIB"
address tso "ALTLIB DEACT APPL(CLIST)"
address tso "FREE FI(IPCSDDIR)"
