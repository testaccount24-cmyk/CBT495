        PROC 0 CMD() OPT() TRAIL() DSNAMES +
               CLIST(FDRPREF.CLIST)  +
               LLIB(FDRPREF.LOAD)  +
               PLIB(FDRPREF.PANELS)  +
               MLIB(FDRPREF.MESSAGES)  +
               SLIB(FDRPREF.SKELETON)  +
               ILIB(FDRPREF.ICL)  +
               JLIB(FDRPREF.JCL)  +
               TOD(12345678) +
               STEPLIB(YES) +
               APPL()
        CONTROL PROMPT MAIN
        CONTROL LIST CONLIST SYMLIST
        IF &STR(&CMD) = VPUT THEN DO
          SET &FDRCLIB = &STR(&CLIST)
          SET &FDRLLIB = &STR(&LLIB)
          SET &FDRPLIB = &STR(&PLIB)
          SET &FDRMLIB = &STR(&MLIB)
          SET &FDRSLIB = &STR(&SLIB)
          SET &FDRILIB = &STR(&ILIB)
          SET &FDRJLIB = &STR(&JLIB)
          SET &INSITO  = &STR(&TOD)
          SET &INSTEP  = &STR(&STEPLIB)
          ISPEXEC VPUT (FDRCLIB FDRLLIB FDRPLIB FDRMLIB FDRSLIB +
            FDRILIB FDRJLIB INSITO INSTEP) SHARED
          EXIT CODE(0)
        END
        IF &STR(&DSNAMES) ¬= THEN DO
          IF &SUBSTR(1:3,&SYSISPF   ) ¬= NOT THEN DO
            ISPEXEC CONTROL ERRORS RETURN
            ISPEXEC VPUT (CLIST LLIB PLIB MLIB SLIB ILIB JLIB +
              STEPLIB) SHARED
            ISPEXEC SELECT CMD(%FDRDSNFC SYSPROC ABRALLOC)
            ISPEXEC SELECT PANEL(FDRDSN)
            IF &LASTCC < 12 THEN EXIT CODE(0)
          END
          CONTROL NOMSG NOFLUSH
          %FDRDSN CLIST('&CLIST') LLIB('&LLIB') PLIB('&PLIB') +
            C1 MLIB('&MLIB') SLIB('&SLIB') ILIB('&ILIB') +
            C2 JLIB('&JLIB') STEPLIB('&STEPLIB')
          IF &LASTCC = 0 THEN EXIT CODE(0)
          LOOP: IF &STR(&CLIST) = THEN GOTO PROMPT
          EXEC '&CLIST(FDRDSN)' 'CLIST(''&CLIST'') LLIB(''&LLIB'') +
            C1 PLIB(''&PLIB'') MLIB(''&MLIB'') SLIB(''&SLIB'') +
            C2 ILIB(''&ILIB'') JLIB(''&JLIB'') STEPLIB(''&STEPLIB'')'
          IF &LASTCC = 0 THEN EXIT CODE(0)
          CONTROL MSG
          PROMPT: WRITE FDRW49 PLEASE ENTER THE FULLY QUALIFIED FDR +
                DIALOG CLIST LIBRARY DATA SET NAME:
          READ &CLIST
          GOTO LOOP
        END
        /*
        ATTN EXIT
        DO WHILE &STR(&CLIST) =  OR &LASTCC ¬=0
          WRITE FDRW49 PLEASE ENTER THE FULLY QUALIFIED FDR +
                DIALOG CLIST LIBRARY DATA SET NAME:
          READ &CLIST
          ALLOC DA('&CLIST') SHR
          END
        DO WHILE &STR(&LLIB) =  OR &LASTCC ¬=0
          WRITE FDRW49 PLEASE ENTER THE FULLY QUALIFIED FDR +
                PROGRAM LIBRARY DATA SET NAME:
          READ &LLIB
          ALLOC DA('&LLIB') SHR
          END
        DO WHILE &STR(&PLIB) =  OR &LASTCC ¬=0
          WRITE FDRW49 PLEASE ENTER THE FULLY QUALIFIED FDR +
                DIALOG PANEL LIBRARY DATA SET NAME:
          READ &PLIB
          ALLOC DA('&PLIB') SHR
          END
        DO WHILE &STR(&MLIB) =  OR &LASTCC ¬=0
          WRITE FDRW49 PLEASE ENTER THE FULLY QUALIFIED FDR +
                DIALOG MESSAGES LIBRARY DATA SET NAME:
          READ &MLIB
          ALLOC DA('&MLIB') SHR
          END
        DO WHILE &STR(&SLIB) =  OR &LASTCC ¬=0
          WRITE FDRW49 PLEASE ENTER THE FULLY QUALIFIED FDR +
                DIALOG SKELETON LIBRARY DATA SET NAME:
          READ &SLIB
          ALLOC DA('&SLIB') SHR
          END
        /*
        IF &STR(&CMD) ¬= OR &STR(&SYSISPF) = ACTIVE THEN DO
          CALL '&LLIB(FDRLOAD)' 'ALTLIB'
          SET &ALTLIB = &LASTCC
          IF &ALTLIB = 1 +
            THEN ALTLIB ACT APPLICATION(CLIST) DATASET('&CLIST')
            ELSE CALL '&LLIB(FDRLOAD)' 'SYSPROC,&CLIST'
          IF &STR(&STEPLIB) = YES THEN DO
            CALL '&LLIB(FDRLOAD)' 'ABRLLIB,&LLIB'
            ISPEXEC LIBDEF ISPLLIB LIBRARY ID(ABRLLIB)
          END
          /* MODIFY LIBDEF STATEMENTS TO USE STACK FOR DYNAMIC ISPF */
          /* CONLEY 2002/08/31                                      */
          ISPEXEC LIBDEF ISPPLIB DATASET ID('&PLIB') STACK
          ISPEXEC LIBDEF ISPMLIB DATASET ID('&MLIB') STACK
          ISPEXEC LIBDEF ISPSLIB DATASET ID('&SLIB') STACK
          IF &STR(&OPT) = THEN DO
            SET &ZTRAIL =
            SET &ZENVIR =
            ISPEXEC VGET (ZTRAIL,ZENVIR)
            IF &STR(&TRAIL) ¬= THEN SET &ZTRAIL = &STR(&TRAIL)
            IF &SUBSTR(6:8,&ZENVIR) = &STR(2.2) +
              THEN ISPEXEC SELECT PANEL(ABRPRIM2) OPT(&STR(&ZTRAIL)) +
                   NEWPOOL
              ELSE ISPEXEC SELECT PANEL(ABRPRIME) OPT(&STR(&ZTRAIL)) +
                   NEWPOOL
            END
          ELSE +
            ISPEXEC SELECT CMD(%ABRINPRI OPT(&STR(&OPT)) APPL(&APPL))
          IF &ALTLIB = 1 THEN ALTLIB DEACTIVATE APPLICATION(CLIST)
          IF &STR(&STEPLIB) = YES THEN DO
            ISPEXEC LIBDEF ISPLLIB
            FREE F(ABRLLIB)
          END
          ISPEXEC LIBDEF ISPPLIB
          ISPEXEC LIBDEF ISPMLIB
          ISPEXEC LIBDEF ISPSLIB
          EXIT CODE(0)
        END
        IF &STR(&STEPLIB) = YES THEN DO
          CONTROL NOMSG
          FREE F(ABRLLIB)
          CONTROL MSG
          CALL '&LLIB(FDRLOAD)' 'ISPLLIB,&LLIB'
        END
        CALL '&LLIB(FDRLOAD)' 'SYSPROC,&CLIST'
        CALL '&LLIB(FDRLOAD)' 'ISPPLIB,&PLIB'
        CALL '&LLIB(FDRLOAD)' 'ISPMLIB,&MLIB'
        CALL '&LLIB(FDRLOAD)' 'ISPSLIB,&SLIB'
        ISPF PANEL(ISR@V3X)
