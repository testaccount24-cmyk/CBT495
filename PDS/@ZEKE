/* rexx */
/*trace i*/
/*********************************************************************/
/* This exec invokes ASG's Zeke/Zebb products dialog.  It assumes    */
/* that the ZEKE loadlib is available via LINKLIST.  If not, add the */
/* ZEKE loadlib to the LINKLIST, or change this exec to use a        */
/* Dynamic STEPLIB product.  It is provided courtesy of Adam         */
/* Vanderhelm at the State of North Carolina and David Ehresman at   */
/* the University of Louisville.                                     */
/*********************************************************************/
parse arg zcmd
address tso "ALTLIB ACT APPL(CLIST) DA('zekepref.ISPCLIB',",
                                      "'zebbpref.ISPCLIB')"
address ispexec "LIBDEF ISPMLIB DATASET ID('zekepref.ISPMLIB',",
                                          "'zebbpref.ISPMLIB',",
                                          "'oasispref.ISPMLIB') STACK"
address ispexec "LIBDEF ISPPLIB DATASET ID('zekepref.ISPPLIB',",
                                          "'zebbpref.ISPPLIB',",
                                          "'oasispref.ISPPLIB') STACK"
address ispexec "LIBDEF ISPTLIB DATASET ID('zekepref.ISPTLIB',",
                                          "'zebbpref.ISPTLIB',",
                                          "'oasispref.ISPTLIB') STACK"
/*********************************************************************/
/* The following code is wrapped around the SELECT PANEL to prevent  */
/* dialog termination if ztrail contains an invalid option.  Instead */
/* of the ISPF dialog termination panel, the "Invalid option" error  */
/* message will be displayed.  This is only necessary for            */
/* non-primary option menus (menus defined with &ZPRIM=NO in the     */
/* )INIT section of the panel) using the IBM default ZSEL syntax.    */
/*********************************************************************/
address ispexec "CONTROL ERRORS RETURN"
do forever
   address ispexec "SELECT PANEL(ALT@PRIM) OPT("zcmd") NEWAPPL(ZEE)",
                   "PASSLIB SCRNAME(ZEKE)"
   if rc <= 4 then
      leave
   else
      do
         address ispexec "SETMSG MSG("zerrmsg")"
         if zerrmsg <> 'ISPD221' then
            leave
      end
   ztrail = ''
end
address ispexec "LIBDEF ISPMLIB"
address ispexec "LIBDEF ISPPLIB"
address ispexec "LIBDEF ISPTLIB"
address tso "ALTLIB DEACT APPL(CLIST)"
