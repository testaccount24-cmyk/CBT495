/* rexx */
/* trace i */
/*********************************************************************/
/* This exec invokes IBM's NPF dialog.  Note:  If you pass a ztrail  */
/* parm to invoke a submenu option of NPF, and then hit end, you     */
/* will return to the invoking applicaton instead of the NPF menu.   */
/* This happens because the NPF Primary Option Menu is not coded as  */
/* a Primary Option Menu (&ZPRIM=NO).  Also, this exec is used       */
/* instead of the NPFINIT default exec, because that exec does not   */
/* use STACK on the LIBDEF commands, which can destroy your existing */
/* LIBDEF's on exit.  Lastly, this exec assumes that TCPIP.SEZALINK  */
/* is available via LINKLIST.  If not, add this loadlib to LINKLIST, */
/* or change this exec to use a Dynamic STEPLIB product.             */
/*********************************************************************/
parse arg ztrail
address ispexec "LIBDEF ISPMLIB DATASET ID('TCPIP.SEZAMENU') STACK"
address ispexec "LIBDEF ISPPLIB DATASET ID('TCPIP.SEZAPENU') STACK"
/*********************************************************************/
/* The following code is wrapped around the SELECT PANEL to prevent  */
/* dialog termination if ztrail contains an invalid option.  Instead */
/* of the ISPF dialog termination panel, the "Invalid option" error  */
/* message will be displayed.  This is only necessary for            */
/* non-primary option menus (menus defined with &ZPRIM=NO in the     */
/* )INIT section of the panel) using the IBM default ZSEL syntax.    */
/*********************************************************************/
address ispexec "CONTROL ERRORS RETURN"
do forever
   address ispexec "SELECT PANEL(EZAPPMP) OPT("ztrail") SCRNAME(NPF)"
   if rc <= 4 then
      leave
   else
      do
         address ispexec "SETMSG MSG("zerrmsg")"
         if zerrmsg <> 'ISPD221' then
            leave
      end
   ztrail = ''
end
address ispexec "LIBDEF ISPMLIB"
address ispexec "LIBDEF ISPPLIB"
