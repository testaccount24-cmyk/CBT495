/* rexx */
/* trace i */
/*********************************************************************/
/* This exec invokes IBM's VTAM Dump Analysis IPCS Dialog.  For z/OS */
/* V1R4 and higher, IBM cleaned up the packaging and placed the      */
/* dialog elements into the IPCS libraries and the VIT load modules  */
/* into SYS1.MIGLIB.  The user profile dataset is used to hold the   */
/* user-customized tables, but you may use any dataset you wish.     */
/* For z/OS V1R3 and lower, this exec assumes you followed the       */
/* implementation instructions in the VTAM install manual, but       */
/* instead of hacking your SMP/E libraries, you allocated separate   */
/* libraries for dialog elements created by the ISPF DTL compiler.   */
/* This exec will turn on PFSHOW since neither release of the VTAM   */
/* Dump Analysis dialogs turns on PFSHOW.                            */
/*********************************************************************/
parse arg ztrail
address ispexec "VGET (ZPFSHOW)"
pfshowsv = zpfshow
address ispexec "SELECT PGM(ISPOPF) PARM(PFK,ON)"
if mvsvar('sysmvs') >= 'SP7.0.4' then
   do
      address tso "ALTLIB ACT APPL(EXEC) DA('SYS1.SBLSCLI0')"
      address tso "ALTLIB ACT APPL(CLIST) DA('SYS1.SBLSCLI0')"
      address ispexec "LIBDEF ISPLLIB DATASET ID('SYS1.MIGLIB') STACK"
      address ispexec "LIBDEF ISPMLIB DATASET ID('SYS1.SBLSMSG0') STACK"
      address ispexec "LIBDEF ISPPLIB DATASET ID('SYS1.SBLSPNL0') STACK"
      address ispexec "LIBDEF ISPTABL DATASET",
                      "ID('"userid()".ISPF.ISPPROF') STACK"
      address ispexec "LIBDEF ISPTLIB DATASET",
                      "ID('"userid()".ISPF.ISPPROF','SYS1.SBLSTBL0')",
                      "STACK"
      address ispexec "SELECT CMD(%ISTDE01 "ztrail") NEWAPPL(ISTD)",
                      "PASSLIB NOCHECK SCRNAME(VTAMDUMP)"
   end
else
   do
      address tso "ALTLIB ACT APPL(CLIST) DA('pref.DYNISPF.EXEC',",
                                            "'SYS1.SISTCLI0',",
                                            "'SYS1.SISTCLS1')"
      address ispexec "LIBDEF ISPLLIB DATASET ID('SYS1.SISTDBUG') STACK"
      address ispexec "LIBDEF ISPMLIB DATASET ID('SYS1.SISTMSG0',",
                                                "'vtamtrac.MESSAGES')",
                                                "STACK"
      address ispexec "LIBDEF ISPPLIB DATASET ID('SYS1.SISTPNL0',",
                                                "'vtamtrac.PANELS')",
                                                "STACK"
      address ispexec "LIBDEF ISPTABL DATASET ID('vtamtrac.TABLES')",
                                                "STACK"
      address ispexec "LIBDEF ISPTLIB DATASET ID('vtamtrac.TABLES')",
                                                "STACK"
      address ispexec "SELECT CMD(%@VTAMDM1 "ztrail") NEWAPPL(ISTD)",
                      "PASSLIB NOCHECK SCRNAME(VTAMDUMP)"
   end
address ispexec "SELECT PGM(ISPOPF) PARM(PFK,"pfshowsv")"
address ispexec "LIBDEF ISPLLIB"
address ispexec "LIBDEF ISPMLIB"
address ispexec "LIBDEF ISPPLIB"
address ispexec "LIBDEF ISPTABL"
address ispexec "LIBDEF ISPTLIB"
address tso "ALTLIB DEACT APPL(CLIST)"
if mvsvar('sysmvs') >= 'SP7.0.4' then
   address tso "ALTLIB DEACT APPL(EXEC)"
