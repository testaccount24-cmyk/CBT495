/* rexx */
/*trace i*/
/*********************************************************************/
/* This exec invokes Vanguard's RACF Administrator dialog.  It is    */
/* provided courtesy of Adam Vanderhelm at the State of North        */
/* Carolina.                                                         */
/*********************************************************************/
parse arg ztrail
address tso "ALLOC FI(VIPOPTS) DA('vanpref.VANOPTS') SHR REUSE"
address tso "ALTLIB ACT APPL(CLIST) DA('vanpref.VANCLIB',",
                                      "'SYS1.HRFCLST')"
address ispexec "LIBDEF ISPMLIB DATASET ID('vanpref.VANMLIB',",
                                          "'SYS1.HRFMSG') STACK"
address ispexec "LIBDEF ISPPLIB DATASET ID('vanpref.VANPLIB',",
                                          "'SYS1.HRFPANL') STACK"
address ispexec "LIBDEF ISPSLIB DATASET ID('vanpref.VANSLIB',",
                                          "'SYS1.HRFSKEL') STACK"
address ispexec "LIBDEF ISPTLIB DATASET ID('vanpref.VANTLIB') STACK"
/*********************************************************************/
/* The following code is wrapped around the SELECT PANEL to prevent  */
/* dialog termination if ztrail contains an invalid option.  Instead */
/* of the ISPF dialog termination panel, the "Invalid option" error  */
/* message will be displayed.  This is only necessary for            */
/* non-primary option menus (menus defined with &ZPRIM=NO in the     */
/* )INIT section of the panel) using the IBM default ZSEL syntax.    */
/*********************************************************************/
address ispexec "CONTROL ERRORS RETURN"
do forever
   address ispexec "SELECT PANEL(RACF2) OPT("ztrail") SCRNAME(VANGUARD)"
   if rc <= 4 then
      leave
   else
      do
         address ispexec "SETMSG MSG("zerrmsg")"
         if zerrmsg <> 'ISPD221' then
            leave
      end
   ztrail = ''
end
address ispexec "LIBDEF ISPMLIB"
address ispexec "LIBDEF ISPPLIB"
address ispexec "LIBDEF ISPSLIB"
address ispexec "LIBDEF ISPTLIB"
address tso "ALTLIB DEACT APPL(CLIST)"
address tso "FREE FI(VIPOPTS)"
